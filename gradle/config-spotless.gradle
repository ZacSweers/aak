/*
 * Copyright (c) 2019 Zac Sweers
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *   http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

apply plugin: 'com.diffplug.gradle.spotless'
String[] copiedJavaFiles = [
    // Add any copied files that should have their copyright header preserved
    "copyright",
].collect { "**/*${it}.java" } as String[]
String[] copiedKotlinFiles = [
    // Add any copied files that should have their copyright header preserved
    "copyright",
].collect { "**/*${it}.kt" } as String[]
spotless {
  format 'misc', {
    target '**/*.md', '**/.gitignore'
    targetExclude "**/.gradle/**"
    targetExclude "**/build/**"
    indentWithTabs()
    trimTrailingWhitespace()
    endWithNewline()
  }
  kotlin {
    target "**/*.kt", "**/*.kt"
    targetExclude(copiedKotlinFiles)
    targetExclude "**/build/**"
    ktlint(deps.versions.ktlint).userData(deps.build.ktlintUserData)
    licenseHeaderFile rootProject.file('spotless/copyright.kt')
    trimTrailingWhitespace()
    endWithNewline()
  }
  kotlinGradle {
    target '*.gradle.kts'
    ktlint(deps.versions.ktlint).userData(deps.build.ktlintUserData)
    trimTrailingWhitespace()
    endWithNewline()
  }
  java {
    target "**/*.java"
    targetExclude(copiedJavaFiles)
    targetExclude "**/build/**"
    googleJavaFormat(deps.versions.gjf)
    licenseHeaderFile rootProject.file('spotless/copyright.java')
    removeUnusedImports()
    trimTrailingWhitespace()
    endWithNewline()
  }
  groovyGradle {
    target '**/*.gradle'
    targetExclude "**/build/**"
    trimTrailingWhitespace()
    endWithNewline()
  }
}
